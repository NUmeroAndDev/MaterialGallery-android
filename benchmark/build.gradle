plugins {
    id 'com.android.test'
    id 'org.jetbrains.kotlin.android'
    alias libs.plugins.baselineProfilePlugin
}

android {
    namespace 'com.numero.material_gallery.benchmark'

    compileSdkVersion versions.compileSdk

    defaultConfig {
        minSdk 28
        targetSdk versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    targetProjectPath = ":app"
    experimentalProperties["android.experimental.self-instrumenting"] = true

    testOptions {
        managedDevices {
            devices {
                pixel2Api31(com.android.build.api.dsl.ManagedVirtualDevice) {
                    device = "Pixel 2"
                    apiLevel = 31
                    systemImageSource = "aosp"
                }
            }
        }
    }
}

baselineProfile {
    managedDevices += "pixel2Api31"
    useConnectedDevices = false
}

dependencies {
    implementation 'androidx.test.ext:junit:1.1.5'
    implementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'androidx.test.uiautomator:uiautomator:2.3.0'
    implementation libs.androidx.benchmark
    implementation libs.androidx.profileinstaller
}