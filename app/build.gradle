plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'dagger.hilt.android.plugin'
    alias libs.plugins.kspPlugin
    alias libs.plugins.baselineProfilePlugin
}

android {
    namespace 'com.numero.material_gallery'

    compileSdkVersion versions.compileSdk

    defaultConfig {
        applicationId "com.numero.material_gallery"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode rootProject.appVersionCode
        versionName rootProject.appVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField("String", "MATERIAL_COMPONENTS_VERSION", "\"${libs.versions.material.get()}\"")
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding = true
        buildConfig = true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation projects.studies.crane
    implementation projects.studies.shrine
    implementation projects.studies.reply
    implementation projects.studies.material2
    implementation projects.studies.material3
    implementation projects.components
    implementation projects.core
    implementation projects.resource

    implementation libs.coroutines.android

    implementation libs.androidx.appcompat
    implementation libs.androidx.activity
    implementation libs.androidx.fragment
    implementation libs.androidx.recyclerview
    implementation libs.androidx.preference
    implementation libs.androidx.constraintlayout
    implementation libs.androidx.core
    implementation libs.androidx.core.splashscreen
    implementation libs.androidx.navigation.fragment
    implementation libs.androidx.navigation.ui
    implementation libs.androidx.lifecycle.runtime
    implementation libs.androidx.lifecycle.viewmodel

    implementation libs.material

    implementation libs.insetter

    implementation libs.dagger.hilt.android
    ksp libs.dagger.hilt.android.compiler

    implementation libs.licenses

    implementation libs.play.core
    implementation libs.play.core.ktx

    implementation libs.androidx.profileinstaller

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'

    "baselineProfile"(projects.benchmark)
}
