buildscript {
    ext {
        versions = [
                compileSdk: 33,
                minSdk    : 23,
                targetSdk : 33,
        ]
    }

    dependencies {
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.6'
    }
}

plugins {
    alias(libs.plugins.androidGradlePlugin) apply false
    alias(libs.plugins.kotlinPlugin) apply false
    alias(libs.plugins.hiltPlugin) apply false
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile)
            .configureEach {
                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_11.toString()
                }
            }
}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 3
    def appMinor = 2
    def appPatch = 0
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}