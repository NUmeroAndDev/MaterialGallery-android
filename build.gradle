buildscript {
    ext {

        versions = [
                compileSdk: 32,
                minSdk    : 23,
                targetSdk : 32,

                navigation: "2.5.0-beta01",
                lifecycle : "2.4.1",

                hilt      : "2.42",

                material  : "1.7.0-alpha02",
        ]

        libs = [
                coroutines: "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.6.2",

                androidx  : [
                        appcompat       : "androidx.appcompat:appcompat:1.4.1",
                        activity        : "androidx.activity:activity-ktx:1.4.0",
                        fragment        : "androidx.fragment:fragment-ktx:1.4.1",
                        core            : "androidx.core:core-ktx:1.7.0",
                        coreSplash      : "androidx.core:core-splashscreen:1.0.0-beta02",
                        recyclerview    : "androidx.recyclerview:recyclerview:1.2.1",
                        constraintlayout: "androidx.constraintlayout:constraintlayout:2.1.3",
                        preference      : "androidx.preference:preference:1.2.0",

                        navigation      : [
                                fragment: "androidx.navigation:navigation-fragment-ktx:$versions.navigation",
                                ui      : "androidx.navigation:navigation-ui-ktx:$versions.navigation",
                        ],

                        lifecycle       : [
                                runtime: "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
                        ],

                        profileinstaller: "androidx.profileinstaller:profileinstaller:1.2.0-rc01"
                ],

                hilt      : [
                        android : "com.google.dagger:hilt-android:$versions.hilt",
                        compiler: "com.google.dagger:hilt-compiler:$versions.hilt"
                ],

                material  : "com.google.android.material:material:$versions.material",
                flexbox   : "com.google.android.flexbox:flexbox:3.0.0",

                insetter  : "dev.chrisbanes.insetter:insetter:0.6.1",

                play      : [
                        core: "com.google.android.play:core:1.10.3",
                        ktx : "com.google.android.play:core-ktx:1.8.1"
                ],

                licenses  : "com.google.android.gms:play-services-oss-licenses:17.0.0"
        ]
    }

    dependencies {
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.5'
    }
}

plugins {
    id 'com.android.application' version '7.2.0' apply false
    id 'com.android.library' version '7.2.0' apply false
    id 'com.android.test' version '7.2.0' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.0' apply false
    id 'com.google.dagger.hilt.android' version '2.43.1' apply false
    id "com.diffplug.spotless" version "6.7.2"
}

subprojects {
    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ratchetFrom("origin/develop")

            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 3
    def appMinor = 1
    def appPatch = 0
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}