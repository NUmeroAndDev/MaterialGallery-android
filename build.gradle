buildscript {
    ext {

        versions = [
                compileSdk: 31,
                minSdk    : 23,
                targetSdk : 31,

                kotlin    : "1.5.21",

                navigation: "2.4.0-alpha07",
                lifecycle : "2.4.0-alpha03",

                hilt      : "2.38.1",

                material  : "1.5.0-alpha03",
        ]

        libs = [
                kotlinPlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",

                coroutines  : "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1",

                androidx    : [
                        appcompat       : "androidx.appcompat:appcompat:1.4.0-alpha03",
                        activity        : "androidx.activity:activity-ktx:1.3.1",
                        fragment        : "androidx.fragment:fragment-ktx:1.4.0-alpha08",
                        core            : "androidx.core:core-ktx:1.7.0-alpha02",
                        coreSplash      : "androidx.core:core-splashscreen:1.0.0-alpha01",
                        recyclerview    : "androidx.recyclerview:recyclerview:1.2.1",
                        constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.4",
                        preference      : "androidx.preference:preference:1.1.1",

                        navigation      : [
                                fragment: "androidx.navigation:navigation-fragment-ktx:$versions.navigation",
                                ui      : "androidx.navigation:navigation-ui-ktx:$versions.navigation",
                        ],

                        lifecycle       : [
                                runtime: "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
                        ]
                ],

                hilt        : [
                        plugin  : "com.google.dagger:hilt-android-gradle-plugin:$versions.hilt",
                        android : "com.google.dagger:hilt-android:$versions.hilt",
                        compiler: "com.google.dagger:hilt-compiler:$versions.hilt"
                ],

                material    : "com.google.android.material:material:$versions.material",
                flexbox     : "com.google.android.flexbox:flexbox:3.0.0",

                insetter    : "dev.chrisbanes.insetter:insetter:0.6.0",

                play        : [
                        core: "com.google.android.play:core:1.10.0",
                        ktx : "com.google.android.play:core-ktx:1.8.1"
                ],

                licenses    : "com.google.android.gms:play-services-oss-licenses:17.0.0"
        ]
    }
    repositories {
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.1.0-alpha10'
        classpath libs.kotlinPlugin
        classpath libs.hilt.plugin
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
    }
}

plugins {
    id("com.diffplug.spotless") version "5.14.3"
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

subprojects {
    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ratchetFrom("origin/develop")

            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 2
    def appMinor = 2
    def appPatch = 0
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}