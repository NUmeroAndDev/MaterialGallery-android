buildscript {
    ext {

        versions = [
                compileSdk: 30,
                minSdk    : 23,
                targetSdk : 30,

                kotlin    : "1.5.10",

                navigation: "2.3.5",
                lifecycle : "2.4.0-alpha01",

                material  : "1.4.0-beta01",
        ]

        libs = [
                kotlinPlugin: "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin",

                androidx    : [
                        appcompat       : "androidx.appcompat:appcompat:1.3.0-rc01",
                        activity        : "androidx.activity:activity-ktx:1.3.0-alpha08",
                        fragment        : "androidx.fragment:fragment-ktx:1.3.3",
                        core            : "androidx.core:core-ktx:1.5.0-beta02",
                        recyclerview    : "androidx.recyclerview:recyclerview:1.2.0",
                        constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.4",
                        preference      : "androidx.preference:preference:1.1.1",

                        navigation      : [
                                fragment: "androidx.navigation:navigation-fragment-ktx:$versions.navigation",
                                ui      : "androidx.navigation:navigation-ui-ktx:$versions.navigation",
                        ],

                        lifecycle       : [
                                livedata: "androidx.lifecycle:lifecycle-livedata-ktx:$versions.lifecycle",
                                runtime : "androidx.lifecycle:lifecycle-runtime-ktx:$versions.lifecycle"
                        ]
                ],

                koin        : "org.koin:koin-androidx-scope:2.1.6",

                material    : "com.google.android.material:material:$versions.material",

                insetter    : "dev.chrisbanes.insetter:insetter:0.6.0",

                play        : [
                        core: "com.google.android.play:core:1.9.0",
                        ktx : "com.google.android.play:core-ktx:1.8.1"
                ],

                licenses    : "com.google.android.gms:play-services-oss-licenses:17.0.0"
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0-beta02'
        classpath libs.kotlinPlugin
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.4'
    }
}

plugins {
    id("com.diffplug.spotless") version "5.12.5"
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenCentral()
    }
}

subprojects {
    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ratchetFrom("origin/master")

            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 2
    def appMinor = 1
    def appPatch = 0
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}