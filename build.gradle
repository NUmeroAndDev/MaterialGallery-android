buildscript {
    ext {

        kotlin_version = '1.3.50'
        material_version = '1.2.0-alpha01'

        kotlinAndroid = [
                stdlib: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
        ]
        androidx = [
                appcompat       : "androidx.appcompat:appcompat:1.1.0",
                activity        : "androidx.activity:activity:1.1.0-rc01",
                fragment        : "androidx.fragment:fragment:1.2.0-rc01",
                ktx             : "androidx.core:core-ktx:1.2.0-beta01",
                recyclerview    : "androidx.recyclerview:recyclerview:1.1.0-rc01",
                constraintlayout: "androidx.constraintlayout:constraintlayout:2.0.0-beta3",
                preference      : "androidx.preference:preference:1.1.0"
        ]

        koin = "org.koin:koin-androidx-scope:2.0.1"

        google = [
                material: "com.google.android.material:material:$material_version",

                playcore: "com.google.android.play:core:1.6.1",

                oss     : "com.google.android.gms:play-services-oss-licenses:17.0.0"
        ]
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.0-beta01'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 1
    def appMinor = 4
    def appPatch = 1
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}