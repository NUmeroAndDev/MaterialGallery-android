buildscript {
    ext {
        versions = [
                compileSdk: 32,
                minSdk    : 23,
                targetSdk : 32,
        ]
    }

    dependencies {
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.5'
    }
}

plugins {
    alias(libs.plugins.androidGradlePlugin) apply false
    alias(libs.plugins.kotlinPlugin) apply false
    alias(libs.plugins.hiltPlugin) apply false
    id "com.diffplug.spotless" version "6.7.2"
}

subprojects {
    // spotless
    apply plugin: "com.diffplug.spotless"
    spotless {
        kotlin {
            target("**/*.kt")
            targetExclude("$buildDir/**/*.kt")
            targetExclude("bin/**/*.kt")

            ratchetFrom("origin/develop")

            ktlint("0.39.0").userData(["disabled_rules": "no-wildcard-imports"])
        }
    }
}

//task clean(type: Delete) {
//    delete rootProject.buildDir
//}

enum VersionSuffix {
    ALPHA(0, "alpha"), BETA(1, "beta"), PRODUCT(9, "")

    private final int code
    private final String name

    VersionSuffix(int code, String name) {
        this.code = code
        this.name = name
    }

    int getCode() {
        code
    }

    String getName() {
        name
    }
}

ext {
    def appMajor = 3
    def appMinor = 1
    def appPatch = 0
    def appBuild = 0

    def suffix = VersionSuffix.PRODUCT

    appVersionCode = appMajor * 1000000 + appMinor * 10000 + appPatch * 1000 + suffix.code * 100 + appBuild
    appVersionName = suffix == VersionSuffix.PRODUCT ?
            "${appMajor}.${appMinor}.${appPatch}" :
            "${appMajor}.${appMinor}.${appPatch}-${suffix.name} ${appBuild}"
}